//! zix grpc .URING dispatch model (Linux): shared-nothing io_uring, one ring +
//! listener per worker (ADR-037 Phase 4 step 3). Mirrors the zix.Http1 ring core:
//! multishot accept, an fd-indexed slot table with a generation-tagged user_data
//! against fd reuse, a plain recv into the per-connection read accumulator, the
//! resumable h2 state machine (core.grpcMuxProcessRing) staging every reply into
//! the connection cork, one coalesced send per readable batch, and a batched CQE
//! drain. Half-duplex per connection (at most one recv or one send in flight).

const std = @import("std");
const core = @import("../core.zig");
const GrpcServerConfig = @import("../config.zig").GrpcServerConfig;
const Route = core.Route;
const common = @import("common.zig");
const epoll_model = @import("epoll.zig");
const logSystem = common.logSystem;
const effectiveCacheEntries = common.effectiveCacheEntries;
const setNoDelay = common.setNoDelay;
const MAX_FD = common.MAX_FD;
const rcache = @import("../../../../utils/response_cache.zig");
const uring = @import("../../../../multiplexers/ring.zig");
const slab = @import("../../../../multiplexers/slab.zig");
const tls_mux = @import("../tls_mux.zig");
const tls_conn = @import("../../../../multiplexers/tls_conn.zig");
const Tls = @import("../../../../tls/Tls.zig");
const reuseport = @import("../../../../multiplexers/reuseport.zig");
const IoUring = std.os.linux.IoUring;

/// SQ entries per worker ring.
const URING_ENTRIES: u16 = 4096;
/// CQ entries per worker ring (larger than the default for multishot headroom).
const URING_CQ_ENTRIES: u32 = 16 * 1024;
/// Max CQEs drained per loop pass.
const URING_CQE_BATCH: usize = 512;

/// io_uring SQPOLL kernel-thread idle before it sleeps, in milliseconds. Inert
/// unless IORING_SETUP_SQPOLL is set (it is not here), kept for when it is.
const URING_SQ_THREAD_IDLE_MS: u32 = 1000;

/// Initialize a worker ring with the single-issuer fast-path flags, falling back
/// to a flagless ring when the kernel does not support them. Mirrors the
/// zix.Http1 ring init.
fn initUringRing() !IoUring {
    const linux = std.os.linux;
    var params = std.mem.zeroInit(linux.io_uring_params, .{
        .flags = linux.IORING_SETUP_SINGLE_ISSUER |
            linux.IORING_SETUP_DEFER_TASKRUN |
            linux.IORING_SETUP_CQSIZE |
            linux.IORING_SETUP_CLAMP,
        .cq_entries = URING_CQ_ENTRIES,
        .sq_thread_idle = URING_SQ_THREAD_IDLE_MS,
    });

    return IoUring.init_params(URING_ENTRIES, &params) catch return IoUring.init(URING_ENTRIES, 0);
}

/// Per-connection ring wrapper. conn holds the h2 state (the rbuf read
/// accumulator and the stage_buf reply cork). gen guards against fd reuse,
/// inflight is the byte count the kernel owns while a send is outstanding, and
/// closing defers the free until the last send lands.
const UringGrpcConn = struct {
    conn: *core.GrpcMuxConn,
    gen: u24,
    inflight: usize,
    closing: bool,
};

/// Ring-hosted TLS connection (dual listener, config.tls_port): the shared TLS connection
/// (tls_mux) plus ring bookkeeping. The ciphertext recv buffer is per connection because the
/// kernel fills it asynchronously (the .EPOLL paths read into per-event stack staging instead).
const UringTlsConn = struct {
    conn: tls_mux.TlsConn,
    gen: u24,
    cipher_buf: []u8,
};

fn freeUringTlsConn(ring_conn: *UringTlsConn) void {
    ring_conn.conn.transport.deinit();
    std.heap.smp_allocator.free(ring_conn.cipher_buf);
    std.heap.smp_allocator.destroy(ring_conn);
}

/// Per-worker fd -> UringTlsConn map for the dual-listener TLS side.
const TlsConnTable = tls_conn.ConnTable(UringTlsConn, MAX_FD, freeUringTlsConn);

const UringMuxCtx = struct {
    io: std.Io,
    ip: []const u8,
    port: u16,
    kernel_backlog: u31,
    opts: core.GrpcServeOpts,
    worker_id: usize,
    busy_poll_us: u32,
    /// Dual-listener TLS side (config.tls + config.tls_port). Inactive when null / 0.
    tls_ctx: ?*Tls.Context = null,
    tls_port: u16 = 0,
    /// CBPF steering wiring (config.reuseport_cbpf). Null = steering off.
    steering: ?reuseport.Steering = null,
};

/// Build a concrete io_uring mux worker entry with the routes baked in at compile
/// time, mirroring epollMuxWorkerFn.
fn uringMuxWorkerFn(comptime routes: []const Route) fn (UringMuxCtx) void {
    return struct {
        const Worker = struct {
            ring: IoUring,
            slots: []?*UringGrpcConn,
            listener_fd: std.posix.fd_t,
            gen_counter: u24,
            opts: core.GrpcServeOpts,
            busy_poll_us: u32,
            /// Dual-listener TLS side (config.tls + config.tls_port). Inactive by default
            /// (-1 / null), so a cleartext-only worker sees zero layout change.
            tls_listener_fd: std.posix.fd_t = -1,
            tls_ctx: ?*Tls.Context = null,
            tls_conns: ?TlsConnTable = null,
            tls_gen_counter: u24 = 0,

            const Self = @This();
            const allocator = std.heap.smp_allocator;
            const linux = std.os.linux;

            fn deinit(self: *Self) void {
                for (self.slots) |maybe_conn| {
                    if (maybe_conn) |gc| {
                        _ = linux.close(gc.conn.fd);
                        gc.conn.deinit();
                        allocator.destroy(gc);
                    }
                }

                slab.unmapSlots(self.slots);
                if (self.tls_conns) |*tls_table| tls_table.deinit();
                self.ring.deinit();
            }

            fn getSqe(self: *Self) ?*linux.io_uring_sqe {
                return self.ring.get_sqe() catch {
                    _ = self.ring.submit() catch return null;

                    return self.ring.get_sqe() catch null;
                };
            }

            fn lookup(self: *Self, decoded: uring.Decoded) ?*UringGrpcConn {
                const idx: usize = @intCast(decoded.fd);
                if (idx >= self.slots.len) return null;

                const gc = self.slots[idx] orelse return null;
                if (gc.gen != decoded.gen) return null;

                return gc;
            }

            fn destroyConn(self: *Self, gc: *UringGrpcConn) void {
                self.slots[@intCast(gc.conn.fd)] = null;

                gc.conn.deinit();
                allocator.destroy(gc);
            }

            fn finishClose(self: *Self, gc: *UringGrpcConn) void {
                _ = linux.close(gc.conn.fd);
                self.destroyConn(gc);
            }

            /// Close intent: flush staged bytes first when possible, otherwise
            /// free now. With a send in flight the free is deferred to the send CQE.
            fn beginClose(self: *Self, gc: *UringGrpcConn) void {
                gc.closing = true;
                if (gc.inflight > 0) return;

                if (gc.conn.stage.len > 0) {
                    self.submitSend(gc);
                    return;
                }

                self.finishClose(gc);
            }

            fn armAccept(self: *Self) void {
                const sqe = self.getSqe() orelse return;
                sqe.prep_multishot_accept(self.listener_fd, null, null, 0);
                sqe.user_data = uring.packUserData(.accept, 0, self.listener_fd);
            }

            fn armRecv(self: *Self, gc: *UringGrpcConn) void {
                const c = gc.conn;

                // Compact the read accumulator before the next recv, mirroring
                // grpcMuxOnReadable's loop-top compaction.
                if (c.rstart == c.rend) {
                    c.rstart = 0;
                    c.rend = 0;
                } else if (c.rend == c.rbuf.len) {
                    const n = c.rend - c.rstart;
                    std.mem.copyForwards(u8, c.rbuf[0..n], c.rbuf[c.rstart..c.rend]);
                    c.rstart = 0;
                    c.rend = n;
                }

                if (c.rend == c.rbuf.len) {
                    self.beginClose(gc);
                    return;
                }

                const sqe = self.getSqe() orelse {
                    self.beginClose(gc);
                    return;
                };
                sqe.prep_recv(c.fd, c.rbuf[c.rend..], 0);
                sqe.user_data = uring.packUserData(.recv, gc.gen, c.fd);
            }

            fn submitSend(self: *Self, gc: *UringGrpcConn) void {
                const c = gc.conn;
                const sqe = self.getSqe() orelse {
                    self.finishClose(gc);
                    return;
                };
                sqe.prep_send(c.fd, c.stage.buf[0..c.stage.len], linux.MSG.NOSIGNAL);
                sqe.user_data = uring.packUserData(.send, gc.gen, c.fd);

                gc.inflight = c.stage.len;
            }

            fn handleAccept(self: *Self, cqe: linux.io_uring_cqe) void {
                const rearm = (cqe.flags & linux.IORING_CQE_F_MORE) == 0;
                defer if (rearm) self.armAccept();

                if (cqe.res < 0) return;

                const conn_fd: std.posix.fd_t = cqe.res;
                const idx: usize = @intCast(conn_fd);
                if (idx >= self.slots.len) {
                    _ = linux.close(conn_fd);
                    return;
                }

                setNoDelay(conn_fd);
                common.setBusyPoll(conn_fd, self.busy_poll_us);

                const c = core.GrpcMuxConn.init(conn_fd, self.opts) orelse {
                    _ = linux.close(conn_fd);
                    return;
                };
                const gc = allocator.create(UringGrpcConn) catch {
                    c.deinit();
                    _ = linux.close(conn_fd);
                    return;
                };

                self.gen_counter +%= 1;
                gc.* = .{ .conn = c, .gen = self.gen_counter, .inflight = 0, .closing = false };
                self.slots[idx] = gc;

                self.armRecv(gc);
            }

            fn handleRecv(self: *Self, cqe: linux.io_uring_cqe, decoded: uring.Decoded) void {
                const gc = self.lookup(decoded) orelse return;

                // res == 0 is a peer hangup, res < 0 a receive error: both close.
                if (cqe.res <= 0) {
                    self.beginClose(gc);
                    return;
                }

                gc.conn.rend += @intCast(cqe.res);

                const outcome = core.grpcMuxProcessRing(routes, gc.conn);

                if (gc.conn.stage.len > 0) {
                    self.submitSend(gc);
                    if (outcome == .close) gc.closing = true;

                    return;
                }

                if (outcome == .close) {
                    self.beginClose(gc);
                    return;
                }

                self.armRecv(gc);
            }

            fn handleSend(self: *Self, cqe: linux.io_uring_cqe, decoded: uring.Decoded) void {
                const gc = self.lookup(decoded) orelse return;

                if (cqe.res < 0) {
                    self.beginClose(gc);
                    return;
                }

                const c = gc.conn;
                const sent: usize = @intCast(cqe.res);
                if (sent < c.stage.len) {
                    std.mem.copyForwards(u8, c.stage.buf[0 .. c.stage.len - sent], c.stage.buf[sent..c.stage.len]);
                    c.stage.len -= sent;
                    gc.inflight = 0;
                    self.submitSend(gc);

                    return;
                }

                c.stage.len = 0;
                gc.inflight = 0;

                if (gc.closing) {
                    self.finishClose(gc);
                    return;
                }

                self.armRecv(gc);
            }

            // ------------------------------------------- //
            // Dual-listener TLS side (config.tls_port): ciphertext recv and the staged-ciphertext
            // flush ride the ring (tls_recv / tls_send ops), the TLS connection logic is
            // tls_mux's (shared with the .EPOLL paths). Half-duplex per connection: while a
            // flush send is in flight no recv is armed, so the transport's staging buffer is
            // never compacted while the kernel reads it.

            fn lookupTls(self: *Self, decoded: uring.Decoded) ?*UringTlsConn {
                const table = if (self.tls_conns) |*tls_table| tls_table else return null;

                const ring_conn = table.get(decoded.fd) orelse return null;
                if (ring_conn.gen != decoded.gen) return null;

                return ring_conn;
            }

            fn armTlsAccept(self: *Self) void {
                const sqe = self.getSqe() orelse return;
                sqe.prep_multishot_accept(self.tls_listener_fd, null, null, 0);
                sqe.user_data = uring.packUserData(.tls_accept, 0, self.tls_listener_fd);
            }

            fn handleTlsAccept(self: *Self, cqe: linux.io_uring_cqe) void {
                const rearm = (cqe.flags & linux.IORING_CQE_F_MORE) == 0;
                defer if (rearm) self.armTlsAccept();

                if (cqe.res < 0) return;

                const conn_fd: std.posix.fd_t = cqe.res;
                const table = if (self.tls_conns) |*tls_table| tls_table else {
                    _ = linux.close(conn_fd);
                    return;
                };
                const idx: usize = @intCast(conn_fd);
                if (idx >= table.slots.len) {
                    _ = linux.close(conn_fd);
                    return;
                }

                setNoDelay(conn_fd);
                // The transport writes records directly (staging on EAGAIN), so the fd must be
                // non-blocking.
                common.setNonBlock(conn_fd);

                const ring_conn = allocator.create(UringTlsConn) catch {
                    _ = linux.close(conn_fd);
                    return;
                };
                const cipher_buf = allocator.alloc(u8, tls_conn.read_staging_size) catch {
                    allocator.destroy(ring_conn);
                    _ = linux.close(conn_fd);
                    return;
                };

                self.tls_gen_counter +%= 1;
                ring_conn.* = .{
                    .conn = .{
                        .transport = tls_conn.Transport.init(conn_fd, self.tls_ctx.?),
                        .opts = self.opts,
                    },
                    .gen = self.tls_gen_counter,
                    .cipher_buf = cipher_buf,
                };
                ring_conn.conn.transport.wbuf_initial = self.opts.tls_write_buf_initial;
                table.put(conn_fd, ring_conn);

                self.armTlsRecv(ring_conn);
            }

            /// Tear a TLS connection down: drop it from the table (frees the conn object) and
            /// hand the fd close to the ring.
            fn closeTls(self: *Self, ring_conn: *UringTlsConn) void {
                const fd = ring_conn.conn.transport.fd;
                if (self.tls_conns) |*tls_table| tls_table.drop(fd);

                const sqe = self.getSqe() orelse {
                    _ = linux.close(fd);
                    return;
                };
                sqe.prep_close(fd);
                sqe.user_data = uring.packUserData(.close, 0, fd);
            }

            fn armTlsRecv(self: *Self, ring_conn: *UringTlsConn) void {
                const sqe = self.getSqe() orelse {
                    self.closeTls(ring_conn);
                    return;
                };
                sqe.prep_recv(ring_conn.conn.transport.fd, ring_conn.cipher_buf, 0);
                sqe.user_data = uring.packUserData(.tls_recv, ring_conn.gen, ring_conn.conn.transport.fd);
            }

            /// Flush the transport's staged ciphertext (wbuf[woff..wlen]) with an on-ring send.
            fn submitTlsSend(self: *Self, ring_conn: *UringTlsConn) void {
                const transport = &ring_conn.conn.transport;

                const sqe = self.getSqe() orelse {
                    self.closeTls(ring_conn);
                    return;
                };
                sqe.prep_send(transport.fd, transport.wbuf[transport.woff..transport.wlen], linux.MSG.NOSIGNAL);
                sqe.user_data = uring.packUserData(.tls_send, ring_conn.gen, transport.fd);
            }

            fn handleTlsRecv(self: *Self, cqe: linux.io_uring_cqe, decoded: uring.Decoded) void {
                const ring_conn = self.lookupTls(decoded) orelse return;

                if (cqe.res <= 0) {
                    self.closeTls(ring_conn);
                    return;
                }

                const keep = tls_mux.onCiphertext(routes, &ring_conn.conn, ring_conn.cipher_buf[0..@intCast(cqe.res)]);
                const transport = &ring_conn.conn.transport;

                if (!keep) {
                    self.closeTls(ring_conn);
                    return;
                }

                // Backpressure-staged ciphertext: flush it on the ring before the next recv.
                if (transport.wlen > transport.woff) {
                    self.submitTlsSend(ring_conn);
                    return;
                }

                if (transport.wclose) {
                    self.closeTls(ring_conn);
                    return;
                }

                self.armTlsRecv(ring_conn);
            }

            fn handleTlsSend(self: *Self, cqe: linux.io_uring_cqe, decoded: uring.Decoded) void {
                const ring_conn = self.lookupTls(decoded) orelse return;
                const transport = &ring_conn.conn.transport;

                if (cqe.res <= 0) {
                    self.closeTls(ring_conn);
                    return;
                }

                transport.woff += @intCast(cqe.res);
                if (transport.woff < transport.wlen) {
                    self.submitTlsSend(ring_conn);
                    return;
                }

                // Drained. Keep the buffer for reuse (freed at close), mirroring the EPOLL flush.
                transport.woff = 0;
                transport.wlen = 0;
                transport.want_out = false;

                if (transport.wclose) {
                    self.closeTls(ring_conn);
                    return;
                }

                self.armTlsRecv(ring_conn);
            }

            // ------------------------------------------- //

            fn run(self: *Self) void {
                self.armAccept();
                if (self.tls_listener_fd != -1) self.armTlsAccept();

                var cqes: [URING_CQE_BATCH]linux.io_uring_cqe = undefined;
                while (true) {
                    _ = self.ring.submit_and_wait(1) catch |err| switch (err) {
                        error.SignalInterrupt => continue,
                        else => return,
                    };

                    const count = self.ring.copy_cqes(&cqes, 0) catch return;
                    for (cqes[0..count]) |cqe| {
                        const decoded = uring.unpackUserData(cqe.user_data);
                        switch (decoded.op) {
                            .accept => self.handleAccept(cqe),
                            .recv => self.handleRecv(cqe, decoded),
                            .send => self.handleSend(cqe, decoded),
                            .timeout => {},
                            .close => {},
                            .tls_accept => self.handleTlsAccept(cqe),
                            .tls_recv => self.handleTlsRecv(cqe, decoded),
                            .tls_send => self.handleTlsSend(cqe, decoded),
                        }
                    }
                }
            }
        };

        fn run(ctx: UringMuxCtx) void {
            // Pin to one cgroup-allowed CPU so workers never oversubscribe a core under a limited cpuset.
            common.pinToCpu(ctx.worker_id);

            // Bind under the order gate: REUSEPORT group index i = worker i,
            // so the cpu-mod-N steering lands on the worker pinned to that slot.
            var bind_turn = reuseport.BindTurn.begin(ctx.steering, ctx.worker_id);
            defer bind_turn.release();

            const addr = std.Io.net.IpAddress.resolve(ctx.io, ctx.ip, ctx.port) catch return;
            var srv = addr.listen(ctx.io, .{
                .reuse_address = true,
                .kernel_backlog = ctx.kernel_backlog,
            }) catch return;
            defer srv.deinit(ctx.io);
            const listener_fd = srv.socket.handle;

            if (ctx.steering) |steer| reuseport.attachCpuSteering(listener_fd, steer.group_size);

            // Dual-listener TLS side: a second listener on tls_port whose connections terminate
            // TLS in this same ring loop (no separate epoll fleet).
            const tls_active = ctx.tls_ctx != null and ctx.tls_port != 0;
            var tls_srv: std.Io.net.Server = undefined;
            var tls_listener_fd: std.posix.fd_t = -1;
            if (tls_active) {
                const tls_addr = std.Io.net.IpAddress.resolve(ctx.io, ctx.ip, ctx.tls_port) catch return;
                tls_srv = tls_addr.listen(ctx.io, .{
                    .reuse_address = true,
                    .kernel_backlog = ctx.kernel_backlog,
                }) catch return;
                tls_listener_fd = tls_srv.socket.handle;
                if (ctx.steering) |steer| reuseport.attachCpuSteering(tls_listener_fd, steer.group_size);
            }
            defer if (tls_active) tls_srv.deinit(ctx.io);

            // Both groups joined: release the bind turn to the next worker.
            bind_turn.release();

            const slots = slab.mapZeroedSlots(?*UringGrpcConn, MAX_FD) catch return;

            var worker = Worker{
                .ring = undefined,
                .slots = slots,
                .listener_fd = listener_fd,
                .gen_counter = 0,
                .opts = ctx.opts,
                .busy_poll_us = ctx.busy_poll_us,
                .tls_listener_fd = tls_listener_fd,
                .tls_ctx = if (tls_active) ctx.tls_ctx else null,
            };
            worker.ring = initUringRing() catch return;
            if (tls_active) worker.tls_conns = TlsConnTable.init() catch return;
            defer worker.deinit();

            // Per-worker unary response cache: lock-free by ownership, never
            // shared, exactly like the EPOLL mux worker.
            var response_cache: rcache.ResponseCache = undefined;
            var cache_on = false;
            if (ctx.opts.response_cache) {
                if (rcache.ResponseCache.init(std.heap.smp_allocator, .{
                    .max_entries = effectiveCacheEntries(ctx.opts),
                    .max_value_bytes = ctx.opts.cache_max_value_bytes,
                })) |built| {
                    response_cache = built;
                    cache_on = true;
                    core.setCache(&response_cache, ctx.opts.cache_ttl_ms);
                } else |_| {
                    cache_on = false;
                }
            }
            defer if (cache_on) {
                core.setCache(null, 0);
                response_cache.deinit();
            };

            worker.run();
        }
    }.run;
}

// --------------------------------------------------------- //
// URING model

/// URING dispatch (Linux-only): shared-nothing, one SO_REUSEPORT listener plus
/// io_uring ring per worker. Each worker multiplexes many connections through the
/// resumable h2 state machine on a completion loop and sends one coalesced reply
/// per readable batch (ADR-037 Phase 4 step 3).
pub fn runUring(comptime routes: []const Route, cfg: GrpcServerConfig) !void {
    // Runtime probe: io_uring can be unavailable on this host (seccomp/sandbox,
    // RLIMIT_MEMLOCK, or an old kernel). Without this, every worker would fail
    // setup, return, and the server would vanish right after binding (a confusing
    // ServerStartTimeout downstream). Fall back to the EPOLL shared-nothing loop.
    var probe = initUringRing() catch |err| {
        logSystem(cfg, "io_uring unavailable ({s}): not suited to this environment (commonly RLIMIT_MEMLOCK, the ulimit -l cap, too low for the ring size). Falling back to EPOLL.", .{@errorName(err)});

        return epoll_model.runEpoll(routes, cfg);
    };
    probe.deinit();

    const io = cfg.io;
    // cgroup-aware so a limited cpuset defaults to one worker per available CPU, not one per machine core.
    const cpu = common.getAvailableCpuCount();
    const worker_count = if (cfg.pool_size == 0) cpu else cfg.pool_size;
    const opts = common.serveOptsWithCache(cfg);

    logSystem(cfg, "listening on {s}:{d} (io_uring-mux/{d})", .{ cfg.ip, cfg.port, worker_count });
    if (cfg.tls != null and cfg.tls_port != 0)
        logSystem(cfg, "dual listener: grpc TLS on {s}:{d} (same workers, on-ring)", .{ cfg.ip, cfg.tls_port });

    const workers = try std.heap.smp_allocator.alloc(std.Thread, worker_count);
    defer std.heap.smp_allocator.free(workers);

    // CBPF steering: one shared bind-order gate, alive until join().
    var bind_gate = reuseport.BindOrderGate{};
    const steering: ?reuseport.Steering = if (cfg.reuseport_cbpf) .{ .gate = &bind_gate, .group_size = worker_count } else null;

    const worker_fn = uringMuxWorkerFn(routes);
    for (workers, 0..) |*thread, idx|
        thread.* = try std.Thread.spawn(
            .{ .stack_size = cfg.worker_stack_size_bytes },
            worker_fn,
            .{UringMuxCtx{
                .io = io,
                .ip = cfg.ip,
                .port = cfg.port,
                .kernel_backlog = cfg.kernel_backlog,
                .opts = opts,
                .worker_id = idx,
                .busy_poll_us = cfg.busy_poll_us,
                .tls_ctx = cfg.tls,
                .tls_port = cfg.tls_port,
                .steering = steering,
            }},
        );

    for (workers) |thread| thread.join();
}
